/*
 * Copyright 2019 Harald Postner.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/**
 * A gradle wrapper that runs the CMake-build-system to compile and link the native C++ source.
 */

plugins {
  //https://plugins.gradle.org/plugin/net.freudasoft.gradle-cmake-plugin
  //https://github.com/freudi74/gradle-cmake-plugin
  id "net.freudasoft.gradle-cmake-plugin" version "0.0.4"
}

ext {
  buildType = 'Release' // or 'Debug'
  // the directory where where the C++ compiler shall deliver the native library.
  deliveryDir = file("${buildDir}/out")
  // the directory where we shall copy the library, so the java build can integrate it into the runtime jar.
  javaResourcesDir = file("${projectDir}/../java/src/main/resources/native/")
}

cmake {
  sourceFolder = project.projectDir
  workingFolder = file("${project.buildDir}/${buildType}")

  buildConfig=buildType

  def.VERSION=project.version
  def.LIB_NAME =  "${rootProject.name}"
  def.DELIVERIES_DIR = deliveryDir.toString()
  def.WITH_UNIT_TESTS = "OFF"
  def.CMAKE_BUILD_TYPE = buildType
}


cmakeBuild {
  dependsOn ':java:compileJava'
}

// copy the library from here to the Java build.
tasks.create('copyLib' , Copy) {
  dependsOn cmakeBuild
  from deliveryDir
  into javaResourcesDir
  include('*.so')
}


cmakeClean {
  doLast {
    delete fileTree("${projectDir}/../java/src/main/resources/native/") {
      include('*.so')
    }
  }
}

task clean(type: Delete) {
  dependsOn cmakeClean
  delete buildDir
}